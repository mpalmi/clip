## <summary>Administrator's unprivileged user role</summary>

########################################
## <summary>
##	Change to the staff role.
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`staff_role_change',`
	gen_require(`
		role staff_r;
	')

	allow $1 staff_r;
')

########################################
## <summary>
##	Change from the staff role.
## </summary>
## <desc>
##	<p>
##	Change from the staff role to
##	the specified role.
##	</p>
##	<p>
##	This is an interface to support third party modules
##	and its use is not allowed in upstream reference
##	policy.
##	</p>
## </desc>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`staff_role_change_to',`
	gen_require(`
		role staff_r;
	')

	allow staff_r $1;
')

########################################
## <summary>
##      Execute all entrypoint files in the staff domain. This
##      is an explicit transition, requiring the
##      caller to use setexeccon().
## </summary>
## <param name="domain">
##      <summary>
##      Domain allowed access.
##      </summary>
## </param>
##
interface(`staff_entry_spec_domtrans',`
        gen_require(`
                        type staff_t;
        ')

        domain_entry_file_spec_domtrans($1, staff_t)
        allow staff_t $1:fd use;
	allow staff_t $1:fifo_file rw_file_perms;
	allow staff_t $1:process sigchld;
')
